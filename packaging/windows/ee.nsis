;Earth Eternal Reborn

;--------------------------------
;Include Modern UI

  !define MUI_WELCOMEFINISHPAGE_BITMAP "sidebar.bmp"
  !define MUI_UNWELCOMEFINISHPAGE_BITMAP "sidebar.bmp"
  !include "MUI2.nsh"
  !include "DirectSetup.nsh"

;--------------------------------
;General

  ;Name and file
  Name "Reborn"
  OutFile ..\..\target\release\EarthEternal_Reborn_Setup.exe

  ;Default installation folder
  InstallDir "$LOCALAPPDATA\Earth Eternal Reborn"
  
  ;Get installation folder from registry if available
  InstallDirRegKey HKCU "Software\Earth Eternal Reborn" ""

  ;Request application privileges for Windows Vista
  RequestExecutionLevel user


;--------------------------------
;Variables

  Var StartMenuFolder
;--------------------------------
;Interface Settings

  !define MUI_ABORTWARNING
  !define MUI_HEADERIMAGE
  !define MUI_HEADERIMAGE_BITMAP "banner.bmp" ; optional

;--------------------------------
;Pages

  !insertmacro MUI_PAGE_WELCOME
  !define MUI_PAGE_CUSTOMFUNCTION_SHOW licpageshow
  !insertmacro MUI_PAGE_LICENSE "License.txt"
  !insertmacro MUI_PAGE_COMPONENTS
  !insertmacro MUI_PAGE_DIRECTORY

;Start Menu Folder Page Configuration
  !define MUI_STARTMENUPAGE_REGISTRY_ROOT "HKCU" 
  !define MUI_STARTMENUPAGE_REGISTRY_KEY "Software\Earth Eternal Reborn" 
  !define MUI_STARTMENUPAGE_REGISTRY_VALUENAME "Earth Eternal Reborn"
  
  !insertmacro MUI_PAGE_STARTMENU Application $StartMenuFolder

  !insertmacro MUI_PAGE_INSTFILES
  !define MUI_FINISHPAGE_NOAUTOCLOSE
  !define MUI_FINISHPAGE_RUN
  !define MUI_FINISHPAGE_RUN_NOTCHECKED
  !define MUI_FINISHPAGE_RUN_TEXT "Start Earth Eternal Reborn"
  !define MUI_FINISHPAGE_RUN_FUNCTION "LaunchLink"
  !define MUI_FINISHPAGE_SHOWREADME_NOTCHECKED
  !define MUI_FINISHPAGE_SHOWREADME $INSTDIR\readme.txt
  
  
  !insertmacro MUI_PAGE_FINISH
  
  !insertmacro MUI_UNPAGE_CONFIRM
  !insertmacro MUI_UNPAGE_INSTFILES
  !insertmacro MUI_UNPAGE_FINISH
  
;--------------------------------
;Languages
 
  !insertmacro MUI_LANGUAGE "English"

;--------------------------------
;Installer Sections

Section "Game Client" GameClient

  SetOutPath "$INSTDIR"
  
  ;ADD YOUR OWN FILES HERE...
  File /r ..\..\common\client\*.*
  File /r ..\..\..\sparkplayer-eartheternal\bin\assets\Resource
  File icon.ico
  File readme.txt
  
  ;Store installation folder
  WriteRegStr HKCU "Software\Earth Eternal Reborn" "" $INSTDIR
  
  ;Install URL handler for web launching
  WriteRegStr HKCU "Software\Earth Eternal Reborn" "" $INSTDIR
  
  ;Create uninstaller
  WriteUninstaller "$INSTDIR\Uninstall.exe"
  
  ;Create shortcuts
  CreateDirectory "$SMPROGRAMS\$StartMenuFolder"
  
  CreateShortCut "$SMPROGRAMS\$StartMenuFolder\Earth Eternal Reborn.lnk" "$INSTDIR\Spark.exe" \
    "http://terraz.mooo.com/Release/Current/EarthEternal.car" \
    $INSTDIR\icon.ico 0 SW_SHOWNORMAL ALT|CONTROL|SHIFT|P "Earth Eternal Reborn"
    
  CreateShortCut "$SMPROGRAMS\$StartMenuFolder\Earth Eternal Reborn (Local Development Server).lnk" "$INSTDIR\Spark.exe" \
    "http://localhost:8080/Release/Current/EarthEternal.car" \
    $INSTDIR\icon.ico 0 SW_SHOWNORMAL ALT|CONTROL|SHIFT|P "Earth Eternal Reborn (Local Development Server)"
    
  CreateShortCut "$SMPROGRAMS\$StartMenuFolder\Uninstall.lnk" "$INSTDIR\Uninstall.exe"
SectionEnd

Section "Direct X9.0 runtime" DX9
  IfSilent +4
      File directx_Jun2010_redist.exe
      ExecWait '"$INSTDIR\directx_Jun2010_redist.exe" "/T:$INSTDIR\DxTmp" /C'
      ${DirectX_Install} "$INSTDIR\DxTmp"
SectionEnd

Section "Visual C++ 2010 runtime" VCR2010
  IfSilent +3
    File vcredist_x86.exe
    ExecWait '"$INSTDIR\vcredist_x86.exe" "/passive" "/norestart"'
SectionEnd

;--------------------------------
;Descriptions

  ;Language strings
  LangString DESC_GameClient ${LANG_ENGLISH} "Game Client."

  ;Assign language strings to sections
  !insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
    !insertmacro MUI_DESCRIPTION_TEXT ${GameClient} $(DESC_GameClient)
  !insertmacro MUI_FUNCTION_DESCRIPTION_END

;--------------------------------
;Uninstaller Section

Section "Uninstall"

  ;ADD YOUR OWN FILES HERE...

  Delete "$INSTDIR\Uninstall.exe"

  RMDir "$INSTDIR"

  !insertmacro MUI_STARTMENU_GETFOLDER Application $StartMenuFolder
    
  Delete "$SMPROGRAMS\$StartMenuFolder\Uninstall.lnk"
  Delete "$SMPROGRAMS\$StartMenuFolder\Earth Eternal Reborn.lnk"
  Delete "$SMPROGRAMS\$StartMenuFolder\Earth Eternal Reborn (Local Development Server).lnk"
  RMDir "$SMPROGRAMS\$StartMenuFolder"

  DeleteRegKey /ifempty HKCU "Software\Earth Eternal Reborn"

SectionEnd

Function licpageshow
    FindWindow $0 "#32770" "" $HWNDPARENT
    CreateFont $1 "Courier New" "$(^FontSize)"
    GetDlgItem $0 $0 1000
    SendMessage $0 ${WM_SETFONT} $1 1
FunctionEnd

Function LaunchLink
  ExecShell "" "$SMPROGRAMS\$StartMenuFolder\Earth Eternal Reborn.lnk"
FunctionEnd